buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

task wrapper (type:Wrapper) {
    gradleVersion = '2.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'groovy'

test {
    testLogging {
        exceptionFormat "full"
    }
}

repositories {
    mavenCentral()
    //jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'com.android.tools.build:gradle:1.1.0-rc1'
    testCompile 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
}

version = "1" // +android-apt.1.4

ext {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    artifactId = 'gapt'
}

group = 'net.sf.gapt'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def maintGradleScript = file("$projectDir/deployment.gradle")
if (maintGradleScript.exists()) {
    apply from: maintGradleScript
}
